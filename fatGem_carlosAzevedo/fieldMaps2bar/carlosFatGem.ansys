!/CLEAR,START ! DO NOT USE THIS COMMAND WHEN USING BATCH JOBS OTHERWISE IT ENTERS IN BEGIN
/PREP7  
! No polynomial elements
/PMETH,OFF,1

! Set electric preferences
KEYW,PR_ELMAG,1
KEYW,MAGELC,1   

! Select element
ET,1,SOLID123

! Material properties
MP,PERX,1,1e10  ! Metal
MP,RSVX,1,0.0   !
MP,PERX,2,1.0   ! Gas
MP,PERX,3,3.0   ! Permittivity of acrilic

! Construct the GEM
!pitch is 5 for 2 and 3 mm diameter holes. 6 for 4 mm diameter holes
pitch =  5   ! Distance between holes, in mm   
holeDiameter = 2
acryl = 4.6   ! Thickness of the acryl layer, in mm  
metal =  0.017   ! Thickness of the meta layers, in mm    
Truedrift = 15
drift =   Truedrift+metal+acryl/2   ! Thickness of the drift region  
Trueinduct = 5
induct = -Trueinduct-metal-acryl/2    ! Thickness of the induction gap     
rim =  holeDiameter/2 +0.2  ! 0.2 rim radius, in mm
                                                                                                                                              

Pressure =  2! Pressure in bar

reducedEfield = -4.628  ! Efield difference across the fat GEM in kV/cm.bar
Edrift= -2.80    ! reduced drift field in V/cm.bar

v_gate = reducedEfield*1000*acryl*0.1*Pressure         !Vtop fatGEM ->acryl*0.1 because acryl unit is mm
v_drift = Edrift*Truedrift*0.1*Pressure+v_gate           ! drift*0.1 because drift unit is mm

v_anode=0 ! baixo 

! Make the plastic (1), lower metal (2), upper metal (3) and gas (4)
BLOCK, 0, pitch/2, 0, sqrt(3)*pitch/2, -acryl/2, acryl/2 ! volume 1 is acryl
BLOCK, 0, pitch/2, 0, sqrt(3)*pitch/2, -acryl/2, -acryl/2-metal ! volume 2 is down metal
BLOCK, 0, pitch/2, 0, sqrt(3)*pitch/2, acryl/2, acryl/2+metal ! volume 3 is upper metal
BLOCK, 0, pitch/2, 0, sqrt(3)*pitch/2, induct, drift ! volume 4 is gas

WPOFFS, 0, 0, -acryl/2-metal
CYL4,   0, 0, holeDiameter/2, ,,, 3*metal+acryl ! volume 5 is first hole   3*metal to insure the metal is cuted
CYL4,   pitch/2,  sqrt(3)*pitch/2, holeDiameter/2, ,,, 3*metal+acryl! volume 6 is second hole

!make hole in acrylic
VSBV,  1,  5, , , KEEP  ! creates 7 (is acryl less cyl 5) empties 1
VSBV,  7,  6, , , KEEP  ! creates 1 (is acryl less cyl 5 and 6) empties 7

!remove cylinders not needed anymore
VDELE, 5, 6

!create new cylinders ro remove rims. use the same thecnique of overestimating length
CYL4,   0, 0, rim, ,,, 3*metal+acryl ! volume 5 is first hole   3*metal to insure the metal is cuted
CYL4,   pitch/2,  sqrt(3)*pitch/2, rim, ,,, 3*metal+acryl! volume 6 is second hole

!remove rims from down metal
VSBV,  2,  5, , , KEEP  ! creates 7 (is metal 2 (down) less cyl 5) empties 2
VSBV,  7,  6, , , KEEP  ! creates 2 (is metal 2 (down) less cyl 5 and 6) empties 7

!remove rims from up metal
VSBV,  3,  5, , , KEEP  ! creates 7 (is metal 3 (upper) less cyl 5) empties 3
VSBV,  7,  6, , , KEEP  ! creates 3 (is metal 3 (upper) less cyl 5 and 6) empties 7

!remove cylinders not needed anymore
VDELE, 5, 6

!remove acrylic from gas
VSBV,  4,  1, , , KEEP    ! creates 5, empties 4
VSBV,  5,  2, , , KEEP    ! creates 4, empties 5
VSBV,  4,  3, , , KEEP    ! creates 5, empties 4


! Glue everything together
VGLUE, ALL


!volumes changed with vglue:
!acryl 7
!down metal
!upper metal
!gas

/COLOR, VOLU, GREEN, 7    !acryl
/COLOR, VOLU, YELLOW, 4     ! Down metal
/COLOR, VOLU, YELLOW, 6    !Upper metal
!/COLOR, VOLU, BLUE, 5    !Gas


! Assign material attributes

! 1 - metal
! 2 - Gas
! 3 - acryl

VSEL, S,,, 4   ! metal atribuido a vol 4
VATT, 1, ,1   
VSEL, S,,, 6     ! metal atribuido a vol 6
VATT, 1, ,1      
VSEL, S,,, 5     ! Gas atribuido a vol 5
VATT, 2, ,1
VSEL, S,,, 7     ! acryl atribuido a vol 7
VATT, 3, ,1
VSEL, S,,, ALL
VPLOT

! Symmetry boundary conditions on the sides
!gas

VSEL, S,,,5 !gas
ASLV, S !new selection set
ASEL, R, LOC, X, 0 ! select surface on posX=0
DA, ALL, SYMM
VSEL, S,,,5
ASLV, S
ASEL, R, LOC, X, pitch/2 !select the opposite surface of the gas
DA, ALL, SYMM
VSEL, S,,,5
ASLV, S
ASEL, R, LOC, Y, 0 !select the gas surface when Y=0
DA, ALL, SYMM
VSEL, S,,,5
ASLV, S
ASEL, R, LOC, Y, +sqrt(3)*pitch/2 !select the gas surface when Y=0
DA, ALL, SYMM

!acryl
VSEL, S,,,7
ASLV, S !new selection set
ASEL, R, LOC, X, 0 ! select surface on posX=0
DA, ALL, SYMM
VSEL, S,,,7
ASLV, S
ASEL, R, LOC, X, pitch/2 !select the opposite surface of the gas
DA, ALL, SYMM
VSEL, S,,,7
ASLV, S
ASEL, R, LOC, Y, 0 !select the gas surface when Y=0
DA, ALL, SYMM
VSEL, S,,,7
ASLV, S
ASEL, R, LOC, Y, +sqrt(3)*pitch/2 !select the gas surface when Y=0
DA, ALL, SYMM


!DOUBTS. is not need to put simetric boundaries in the metal?

! Voltage boundaries on the drift and induction plane

ASEL, S, LOC, Z, drift
DA, ALL, VOLT, v_drift	
ASEL, S, LOC, Z, induct   
DA, ALL, VOLT, v_anode     
       

! Voltage boundary conditions on the metal
VSEL, S,,, 6 !upper metal
ASLV, S
DA, ALL, VOLT, v_gate
VSEL, S,,, 4 !down metal
ASLV, S
DA, ALL, VOLT, v_anode

! Meshing options
VSEL, S,,, 5 !gas select all?
VSEL, A,,, 7 !acryl

!NOTE: MESHING METALS INCREASE THE COMPUTING TIME. A LOT!!!!
!VSEL, A,,, 4 !metal
!VSEL, A,,, 6 !metal
ASLV, S

MSHKEY,0
SMRTSIZE, 5
VMESH,ALL




! Solve the field
/SOLU
SOLVE   
FINISH  



! Display the solution
/POST1  
/EFACET,1   
PLNSOL, VOLT,, 0

! Write the solution to files
/OUTPUT, PRNSOL, lis
PRNSOL
/OUTPUT

/OUTPUT, NLIST, lis
NLIST,,,,COORD
/OUTPUT

/OUTPUT, ELIST, lis
ELIST
/OUTPUT

/OUTPUT, MPLIST, lis
MPLIST
/OUTPUT

/EXIT,NOSAVE

